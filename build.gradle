plugins {
    id 'application'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'java'
    id("com.gradleup.shadow") version "9.1.0"
}

group = 'org.pomotimo'
version = '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

application {
    mainClass.set('org.pomotimo.Launcher')
    mainModule.set('org.pomotimo')
    applicationDefaultJvmArgs = []
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.openjfx:javafx-controls:22.0.1'
    implementation 'org.openjfx:javafx-base:22.0.1'
    implementation 'org.openjfx:javafx-fxml:22.0.1'
    implementation 'org.openjfx:javafx-graphics:22.0.1'

    //Icons for title bar etc.
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'

    // unit tests
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // java native access (e.g. for window snapping in windows)
    implementation 'net.java.dev.jna:jna:5.17.0'
    implementation 'net.java.dev.jna:jna-platform:5.17.0'

    //google gson for object serialization
    implementation 'com.google.code.gson:gson:2.13.1'

    // logger, https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation("org.slf4j:slf4j-api:2.0.17")
    runtimeOnly 'ch.qos.logback:logback-classic:1.4.11'
}

test {
    useJUnitPlatform()
}

javafx {
    version = "22.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing' ]
}

run {
    jvmArgs = [
            '--module-path', configurations.runtimeClasspath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.web,javafx.swing'
    ]
}

task createJavadoc(type: Javadoc) {
    destinationDir = file("build/docs/javadoc")
    source = sourceSets.main.allJava
    classpath += configurations.compileClasspath
}

task buildAll(type: GradleBuild) {
    tasks = ['build', 'createJavadoc']
    doLast {
        // additional actions after the build
    }
}
jar {
    manifest {
        attributes(
                'Main-Class': 'org.pomotimo.Launcher'
        )
    }
}
shadowJar {
    archiveBaseName.set('pomotimo')
    archiveVersion.set('1.0')
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.pomotimo.Launcher'
    }
}

task jpackageApp(type: Exec) {
    dependsOn shadowJar
    def jarName = "pomotimo-1.0.jar"
    def appName = "Pomotimo"

    commandLine "jpackage",
            "--input", "build/libs",
            "--main-jar", jarName,
            "--main-class", "org.pomotimo.Launcher",
            "--name", appName,
            "--type", "exe",
            "--win-dir-chooser",
            "--win-menu",
            "--win-shortcut",
            "--icon", "assets/app_icon_2.ico",
            "--dest", "./build/jpackage"
}
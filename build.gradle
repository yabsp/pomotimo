plugins {
    id 'application'
    id 'checkstyle'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.3'
    id 'java'
    id("com.gradleup.shadow") version "9.1.0"
}

group = 'org.pomotimo'
version = '1.2'

repositories {
    mavenCentral()
}

ext {
    appName = "Pomotimo"
    appVersion = version
    javafxVersion = '22.0.1'
    junitVersion = '5.10.0'
    ikonliVersion = '12.3.1'
    slf4jVersion = '2.0.17'
    logbackVersion = '1.4.11'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

application {
    mainModule = 'org.pomotimo'
    mainClass = 'org.pomotimo.gui.PomotimoGUI'
}

dependencies {
    implementation "org.openjfx:javafx-controls:${javafxVersion}"
    implementation "org.openjfx:javafx-base:${javafxVersion}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}"

    //Icons for title bar etc.
    implementation "org.kordamp.ikonli:ikonli-javafx:${ikonliVersion}"
    implementation "org.kordamp.ikonli:ikonli-fontawesome5-pack:${ikonliVersion}"

    // unit tests
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"

    // java native access (e.g. for window snapping in windows)
    implementation "net.java.dev.jna:jna:5.17.0"
    implementation "net.java.dev.jna:jna-platform:5.17.0"

    //google gson for object serialization
    implementation "com.google.code.gson:gson:2.13.1"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    runtimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
}

test {
    useJUnitPlatform()
}

javafx {
    version = "22.0.1"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing']
}

run {
    jvmArgs = [
            '--module-path', configurations.runtimeClasspath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.web,javafx.swing'
    ]
}

task createJavadoc(type: Javadoc) {
    destinationDir = file("build/docs/javadoc")
    source = sourceSets.main.allJava
    classpath += configurations.compileClasspath
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'org.pomotimo.gui.PomotimoGUI'
        )
    }
}
shadowJar {
    archiveBaseName.set(appName.toLowerCase())
    archiveVersion.set(appVersion)
    archiveClassifier.set('')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.pomotimo.gui.PomotimoGUI'
    }
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'pomotimo'
    }
    jpackage {
        resourceDir = file("src/main/resources/linux-packaging")
        imageName = 'Pomotimo'

        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.isWindows()) {
            icon = 'src/main/resources/icons/alt/app_icon.ico'
            installerType = 'exe'
            installerOptions = ['--win-menu', '--win-shortcut', '--win-dir-chooser']
        } else if (os.isLinux()) {
            icon = 'src/main/resources/icons/alt/app_icon.png'
            installerType = 'deb' // or 'rpm'
            installerOptions = ['--linux-shortcut',
                                '--linux-deb-maintainer', 'pomotimo-dev']
        } else if (os.isMacOsX()) {
            icon = 'src/main/resources/icons/alt/app_icon.icns'
            installerType = 'dmg'
            installerOptions = ['--mac-package-name', 'Pomotimo']
        }
    }
}

tasks.jpackageImage.doLast {
    copy {
        from("src/main/resources/linux-packaging/pomotimo.desktop")
        into("build/jpackage/Pomotimo/lib")
    }
    println "Overwrote generated .desktop file with custom version."
}